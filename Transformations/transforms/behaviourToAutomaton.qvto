modeltype InteractionTable uses 'interactionTable';
modeltype BehaviourTable uses 'behaviourTable';
modeltype Automaton uses 'ioAutomaton';

transformation behaviourToAutomaton(in bTable : BehaviourTable, out automaton : Automaton);

main() {
	bTable.objectsOfKind(State).map transformState();
	bTable.objectsOfKind(CommObject).map transformObj();
	bTable.objectsOfKind(Operation).map transformOperation();
	
	bTable.objectsOfKind(Activation).map transformActivation();

	bTable.objectsOfKind(BehaviourPackage).map transformBehPackage();
}

mapping BehaviourPackage::transformBehPackage() : AutomatonPackage {
	automatons += bTable.objectsOfType(CommObject).map buildAutomaton();
}

mapping State::transformState() : State {
	name := self.name;
}

mapping CommObject::transformObj() : CommObject {
	name := self.name
}

mapping Activation::transformActivation() : Activation {
	outMessages := self.outMessages.map transformOutMessage();
	returnType := self.returnType;
	name := self.name;
}

mapping Operation::transformOperation() : Operation {
	name := self.name;
}

mapping OutMessage::transformOutMessage() : OutMessage {
	operation := self.operation.resolveone(Operation);
	returnType := self.returnType;
	recvObject := self.recvObject.resolveone(CommObject);
}

mapping Behaviour::transformBehaviour() : Behaviour {
	inMessage := self.inMessage;
	preState := self.preState.resolveone(State);
	postState := self.postState.resolveone(State);
	activation := self.activation.resolveone(Activation);
}

mapping CommObject::buildAutomaton() : IOAutomaton {
	// get all behaviours the CommObject is involved in as a receiver and build an automaton out of it
	var behTables := bTable.objectsOfType(BehaviourTable::BehaviourTable)->select(b | b.actor = self);
	var myBehaviours := behTables.behaviours;
	
	actor := behTables->any(true).actor.resolveone(CommObject);	
	states := myBehaviours.preState.resolve(State);	
	transitions := myBehaviours.map transformBehaviour();
	
	
	
	while(behTables->size() > 1) {
		var curTable := behTables->any(true);
		var stillPossible := behTables->select(b | not curTable.behaviours.preState->includes(b.behaviours->at(1).preState));
		behTables := stillPossible;
	};
	start := behTables->any(true).behaviours->at(1).preState.resolveone(State);	
	result.print();
	
}

helper IOAutomaton::print() {
	log("====AUTOMATON====");
	log("object: " + self.actor.name);
	log("start: " + self.start.name);
	log("");
	self.transitions->collect(t | log("\t" + t.preState.name + " -> " + t.postState.name + ": " + t.inMessage.name));
	log("");
	
}